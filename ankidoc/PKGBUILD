# Maintainer: Timo Fr√ºh <dev@tifrueh.ch>
pkgname=ankidoc
pkgver=0.3.0_rc1
pkgrel=1
pkgdesc="convert asciidoc notes to anki notes"
arch=('x86_64' 'aarch64')
url="https://github.com/tifrueh/ankidoc/"
license=('GPL-3.0-or-later')
depends=('asciidoctor')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel' 'python-setuptools-scm')
provides=('ankidoc')
source=("https://github.com/tifrueh/$pkgname/archive/refs/tags/v${pkgver//_/-}.tar.gz")
sha256sums=('ece12b96d6ae4a6130bc998ace08cd95cab7cd4b1fc3ee6b70e6f63034783c91')

build() {
	cd "$pkgname-${pkgver//_/-}"
	python -m build --wheel --no-isolation
}

package() {
	cd "$pkgname-${pkgver//_/-}"
	python -m installer --destdir="$pkgdir" dist/*.whl
}
